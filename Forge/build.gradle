plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'io.github.CDAGaming.cursegradle' version '1.6.1'
}

archivesBaseName = "${mod_id}-forge-${minecraft_version}"

minecraft {
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    mappings channel: 'official', version: minecraft_version
    
    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                    source project(':Common').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                    source project(':Common').sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modDataRun {
                    source sourceSets.main
                    source project(':Common').sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"
    config "${mod_id}.forge.mixins.json"
    debug.export = true
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    implementation fg.deobf("com.diontryban.ash:ash-forge-${minecraft_version}:${ash_version}")

    compileOnly project(':Common')
}

tasks.withType(JavaCompile) {
    source(project(':Common').sourceSets.main.allSource)
}

processResources {
    from project(':Common').sourceSets.main.resources
}


jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }
    repositories {
        maven {
            url 'file://' + System.getenv('local_maven')
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'zp4jb9oQ'
    versionNumber = "${project.version}-forge"
    versionName = "[Forge ${project.minecraft_version}] v${project.version}"
    versionType = 'release'
    changelog = rootProject.file('MINI_CHANGELOG.md').text
    uploadFile = jar
    gameVersions = ['1.20']
    loaders = ['forge']
    dependencies {
        required.project 'ash-api'
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_TOKEN')
    project {
        id = '377582'
        releaseType = 'release'
        changelog = rootProject.file('MINI_CHANGELOG.md').text
        changelogType = 'markdown'
        mainArtifact(jar) {
            it.displayName = "[Forge ${project.minecraft_version}] v${project.version}"
        }
        addGameVersion 'Forge'
        addGameVersion 'Client'
        addGameVersion 'Java 17'
        addGameVersion '1.20'
        relations {
            it.requiredDependency 'ash-api'
        }
    }
}
