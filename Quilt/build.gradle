plugins {
    id 'org.quiltmc.loom' version '1.+'
    id 'maven-publish'
    id 'idea'
    id 'com.modrinth.minotaur' version '2.+'
    id 'io.github.CDAGaming.cursegradle' version '1.6.1'
}

archivesBaseName = "${mod_id}-quilt-${minecraft_version}"

repositories {
    maven {
        name = 'TarraformersMC'
        url = 'https://maven.terraformersmc.com/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        mappings "org.quiltmc:quilt-mappings:${quilt_mappings_version}:intermediary-v2"
        officialMojangMappings()
    }

    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"
    modApi "org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_version}"
    modApi "com.terraformersmc:modmenu:${modmenu_version}"

    modImplementation "com.diontryban.ash:ash-quilt-${minecraft_version}:${ash_version}"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(':Common')
}

loom {
    runs {
        client {
            client()
            setConfigName('Quilt Client')
            ideConfigGenerated(true)
            runDir('run')
        }
        server {
            server()
            setConfigName('Quilt Server')
            ideConfigGenerated(true)
            runDir('run')
        }
    }
    accessWidenerPath = file("${project(':Common').projectDir.path}/src/main/resources/${mod_id}.accesswidener")
    mixin {
        defaultRefmapName = "${mod_id}.refmap.json"
    }
}

processResources {
    from project(':Common').sourceSets.main.resources
    inputs.property 'version', project.version

    filesMatching('quilt.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(':Common').sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url 'file://' + System.getenv('local_maven')
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'zp4jb9oQ'
    versionNumber = "${project.version}-quilt"
    versionName = "[Quilt ${project.minecraft_version}] v${project.version}"
    versionType = 'release'
    changelog = rootProject.file('MINI_CHANGELOG.md').text
    uploadFile = remapJar
    gameVersions = ['1.20']
    loaders = ['quilt']
    dependencies {
        required.project 'ash-api'
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_TOKEN')
    project {
        id = '377582'
        releaseType = 'release'
        changelog = rootProject.file('MINI_CHANGELOG.md').text
        changelogType = 'markdown'
        mainArtifact(remapJar) {
            it.displayName = "[Quilt ${project.minecraft_version}] v${project.version}"
        }
        addGameVersion 'Quilt'
        addGameVersion 'Client'
        addGameVersion 'Java 17'
        addGameVersion '1.20'
        relations {
            it.requiredDependency 'ash-api'
        }
    }
}
